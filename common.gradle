allprojects { subproject ->
    group = 'com.company.aem'
    version = '1.0.0-SNAPSHOT'

    apply plugin: "eclipse"
    apply plugin: "idea"

    repositories {
        maven { url "https://repo.adobe.com/nexus/content/groups/public" }
        maven { url "https://repo1.maven.org/maven2" }
        maven { url  "https://dl.bintray.com/neva-dev/maven-public" }
        jcenter()
    }

    plugins.withId 'com.cognifide.aem.base', {

        // Common AEM configuration (CRX packages, deployment etc)

        aem {
            config {
                localAuthorInstance()
                localPublishInstance()
                awaitCondition = { it.bundleState.activeIgnoring("org.apache.sling.jcr.webdav") }
            }
        }

    }

    plugins.withId 'kotlin', {
        def compileOptions = {
            kotlinOptions.jvmTarget = "1.8"
        }

        compileKotlin compileOptions
        compileTestKotlin compileOptions
    }

    plugins.withId 'com.cognifide.aem.bundle', {

        // Unified bundle configuration

        jar {
            manifest {
                attributes([
                        'Bundle-Category': 'example',
                        'Bundle-Vendor': 'Company'
                ])
            }
        }



        dependencies {
            // AEM runtime dependencies

            compileOnly group: 'org.osgi', name: 'osgi.cmpn', version: '6.0.0'
            compileOnly group: 'javax.servlet', name: 'servlet-api', version: '2.5'
            compileOnly group: 'javax.jcr', name: 'jcr', version: '2.0'
            compileOnly group: 'org.slf4j', name: 'slf4j-api', version: '1.7.21'
            compileOnly group: 'org.apache.geronimo.specs', name: 'geronimo-atinject_1.0_spec', version: '1.0'
            compileOnly group: 'org.apache.sling', name: 'org.apache.sling.api', version: '2.16.2'
            compileOnly group: 'org.apache.sling', name: 'org.apache.sling.jcr.api', version: '2.4.0'
            compileOnly group: 'org.apache.sling', name: 'org.apache.sling.models.api', version: '1.3.2'
            compileOnly group: 'com.google.code.gson', name: 'gson', version: '2.3'
            compileOnly group: 'com.adobe.aem', name: 'uber-jar', version: '6.3.0', classifier: 'obfuscated-apis'
            compileOnly group: 'joda-time', name: 'joda-time', version: '2.9.1'

            // Extra libraries provided by packages through task 'aemSatisfy'
            // or configurations: 'aemEmbed', 'aemInstall'.

            compileOnly group: 'org.jetbrains.kotlin', name: 'kotlin-osgi-bundle', version: '1.2.21'
            compileOnly group: 'org.hashids', name: 'hashids', version: '1.0.1'

            // Tests

            testImplementation group: 'junit', name: 'junit', version: '4.12'
            testImplementation group: 'io.wcm', name: 'io.wcm.testing.aem-mock', version: '2.2.8'
        }
    }

    plugins.withId 'com.moowork.node', {
        node {
            version = '6.11.1'
            yarnVersion = '0.27.5'
            download = true
        }
    }
}
