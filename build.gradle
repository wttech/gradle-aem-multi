plugins {
    id 'com.neva.fork'
    id 'com.cognifide.aem.package'
    id 'com.cognifide.aem.instance'
}

description = 'Example'
defaultTasks = [':aemSatisfy', ':aemDeploy', ':aemAwait']

apply from: 'common.gradle'

aemCompose {
    includeProjects ':app:*'
    includeProjects ':content:*'
}

aemSatisfy {
    packages {
        group 'dep.vanity-urls', { /* local("pkg/vanityurls-components-1.0.2.zip") */ }
        group 'dep.kotlin', { dependency('org.jetbrains.kotlin:kotlin-osgi-bundle:1.2.21') }
        group 'tool.search-webconsole-plugin', { dependency('com.neva.felix:search-webconsole-plugin:1.2.0') }
        group 'tool.groovy-console', { url('https://github.com/OlsonDigital/aem-groovy-console/releases/download/11.0.0/aem-groovy-console-11.0.0.zip') }
    }
}

fork {
    config {
        cloneFiles()
        moveFiles([
                "/com/company/aem/example": "/{{projectGroup|substitute('.', '/')}}/{{projectName}}",
                "/example": "/{{projectName}}"
        ])
        replaceContents([
                "com.company.aem.example": "{{projectGroup}}.{{projectName}}",
                'com.company.aem': "{{projectGroup}}",
                "Example": "{{projectLabel}}",
                "example": "{{projectName}}",
        ])
    }
    inPlaceConfig 'setup', {
        copyTemplateFile("gradle.properties")
    }
}
