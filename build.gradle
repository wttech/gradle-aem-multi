buildscript {
    apply from: "$rootProject.projectDir/gradle/buildscript.gradle", to: buildscript
}

description = "Example"
defaultTasks = ['aemBuild']

apply plugin: 'com.cognifide.aem.package'
apply plugin: 'com.cognifide.aem.instance'

allprojects { subproject ->
    group = 'com.company.aem'
    version = '1.0.0-SNAPSHOT'

    apply plugin: "eclipse"
    apply plugin: "idea"

    repositories {
        maven { url "https://repo.adobe.com/nexus/content/groups/public" }
        maven { url "https://repo1.maven.org/maven2" }
        jcenter()
        mavenCentral()
        mavenLocal()
    }

    plugins.withId 'com.cognifide.aem.base', {

        // Common AEM configuration (CRX packages, deployment etc)

        aem {
            config {
                localInstance("http://localhost:4502")
                localInstance("http://localhost:4503")
            }
        }

    }

    plugins.withId 'java', {

        sourceCompatibility = 1.8
        targetCompatibility = 1.8

        def compileOptions = {
            options.encoding = "UTF-8"
            options.compilerArgs << '-Xlint:deprecation'
        }

        compileJava compileOptions
        compileTestJava compileOptions

    }

    plugins.withId 'org.dm.bundle', {

        // Unified bundle configuration

        bundle {
            instruction 'Bundle-Name', subproject.description
            instruction 'Bundle-Category', 'example'
            instruction 'Bundle-Vendor', 'Example'
        }

        // AEM runtime dependencies

        dependencies {
            testCompile group: 'junit', name: 'junit', version: '4.8.1'

            compile group: 'org.osgi', name: 'osgi.cmpn', version: '6.0.0'
            compile group: 'javax.servlet', name: 'servlet-api', version: '2.5'
            compile group: 'javax.jcr', name: 'jcr', version: '2.0'
            compile group: 'org.slf4j', name: 'slf4j-api', version: '1.5.10'
            compile group: 'org.apache.geronimo.specs', name: 'geronimo-atinject_1.0_spec', version: '1.0'
            compile group: 'org.apache.sling', name: 'org.apache.sling.api', version: '2.2.0'
            compile group: 'org.apache.sling', name: 'org.apache.sling.jcr.api', version: '2.1.0'
            compile group: 'org.apache.sling', name: 'org.apache.sling.models.api', version: '1.1.0'
            compile group: 'com.google.code.gson', name: 'gson', version: '2.3'
            compile group: 'com.adobe.aem', name: 'uber-jar', version: '6.2.0', classifier: 'obfuscated-apis'
            compile group: 'joda-time', name: 'joda-time', version: '2.9.1'
        }

    }

    plugins.withId 'com.moowork.node', {
        node {
            version = '6.11.1'
            yarnVersion = '0.27.5'
            download = true
        }
    }
}

aemSatisfy {
    packages {
        group 'dependencies', {
            // local("pkg/vanityurls-components-1.0.2.zip")
        }

        group 'tools', {
            url('https://github.com/OlsonDigital/aem-groovy-console/releases/download/9.0.1/aem-groovy-console-9.0.1.zip')
            url("https://github.com/Cognifide/APM/releases/download/cqsm-2.0.0/apm-2.0.0.zip")
        }
    }

}

aemCompose {
    includeProjects ':app:*'
    includeProjects ':content:*'
}
